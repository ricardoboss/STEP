function isEven = (number n) {
	return n % 2 == 0
}

function calculateCircleArea = (number radius) {
	if (radius <= 0.0) {
		println("Error: The radius must be greater than zero")
		return 0.0
	} else {
		return pi * radius ** 2
	}
}

function printEvenNumbers = (number limit) {
	number i = 1
	while (i <= limit) {
		if (isEven(i)) {
			println(i, " is even")
		}
		i = i + 1
	}
}

function add = (number a, number b = 0) {
	return a + b
}

function loop = (bool whileLoop, bool breakOrReturn) {
	if (whileLoop) {
		number i = 0
		while (i < 10) {
			i = i + 1

			if (i > 5) {
				if (breakOrReturn) {
					break
				} else {
					return i
				}
			}
		}

		return 0
	}

	foreach (number i in [0, 1, 2, 3, 4, 5, 6, 7, 8, 9]) {
		if (i > 5) {
			if (breakOrReturn) {
				break
			} else {
				return i
			}
		}
	}

	return 0
}

function generateClosureWithCapturedVariable = (number x) {
	function inner = (number y) {
		return x + y
	}

	return inner
}

println("Inline function definition and call:")
println((number a, number b) {
	return a + b
}(1, 2))
println()

number radius = 5.0
number pi = 3.141592653589793238462643383279502884197169399375105820974944592307816406286
number area = calculateCircleArea(radius)
println("The area of the circle with radius ", radius, " is ", area)
println()

println("Even numbers from 1 to 10:")
printEvenNumbers(10)

println(toString(isEven(12)))

println("with default parameter:")
println(add(1))

println("with default parameter but another value:")
println(add(1, 2))

println("loop(true, true): ", loop(true, true))
println("loop(true, false): ", loop(true, false))
println("loop(false, true): ", loop(false, true))
println("loop(false, false): ", loop(false, false))

println()
print("closure with captured variable: ")
number x = 5 // variable names are explicitly different from the parameters so they are not used from global scope
number y = 10

function captured = generateClosureWithCapturedVariable(x)
println(captured(y))
